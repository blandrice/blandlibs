#include <DJ_head.krnk>

# ===================================================================
# Library: doublejump\dj_oldsync\old_djump_client.krnk
# Author: blandrice
# ===================================================================

obj updateState = { updateId: 0, playerUpdateId: 0, lastUpdate: 0 };
obj currentPlayerState = {};
obj[] playerStateHistory = obj[];

obj action findHistoryState() {
    # Current state that was synced up was the last state
    obj currentState = playerStateHistory[lengthOf playerStateHistory - 1];
    obj[] possibleStates = obj[];
    num highestMatches = 0;

    # check all 6 position/velocity numbers will match a state
    for(num i = lengthOf playerStateHistory - 2; i >= 0; i--) {
        obj historyState = playerStateHistory[i];
        num totalMatches = 0;
        num tolerance = 0.01;

        if(Math.abs((num)historyState.position.x - (num)currentState.position.x) < tolerance) { totalMatches++; } 
        if(Math.abs((num)historyState.position.y - (num)currentState.position.y) < tolerance) { totalMatches++; } 
        if(Math.abs((num)historyState.position.z - (num)currentState.position.z) < tolerance) { totalMatches++; } 
        if(Math.abs((num)historyState.velocity.x - (num)currentState.velocity.x) < tolerance) { totalMatches++; } 
        if(Math.abs((num)historyState.velocity.y - (num)currentState.velocity.y) < tolerance) { totalMatches++; } 
        if(Math.abs((num)historyState.velocity.z - (num)currentState.velocity.z) < tolerance) { totalMatches++; } 

        if(totalMatches == 6) {
			# GAME.log(lengthOf playerStateHistory - i);
            return playerStateHistory[i];
        }
        if(totalMatches > highestMatches) {
            highestMatches = totalMatches;
        }
        addTo possibleStates {state: playerStateHistory[i], totalMatches: totalMatches, id: lengthOf playerStateHistory - i };
    }
    # Rest is error logging
    GAME.log("Failed to find state history", GAME.TIME.now());
	GAME.log("Highest matches: ", highestMatches);
    for(num i = 0; i < lengthOf possibleStates; i++) {
        if((num)possibleStates[i].totalMatches == highestMatches) {
            GAME.log("Index: ", possibleStates[i].id);
            return (obj)possibleStates[i].state;
        }
    }
    return currentPlayerState;
}

bool syncStarted = false;
# Runs every game tick
public action update(num delta) {
    syncStarted = false;
}

num jumpCount = 0;
bool hasJumped = false;
num timeLastCrouched = GAME.TIME.now();

# Player update
public action onPlayerUpdate(str id, num delta, obj inputs) {
    obj player = GAME.PLAYERS.getSelf();
    num timeNow = GAME.TIME.now();
    (num) updateState.playerUpdateId++;

    # Syncing: We got a sync, so reset the current player state to the correct history
    if(!syncStarted && timeNow == (num)updateState.lastUpdate)
    {
        syncStarted = true;
        currentPlayerState = findHistoryState();
        # sync done, throw out old values
        playerStateHistory = obj[];
    }

    # Double-Jump stuff
    if ((bool) inputs.crouch) {
        player.timeLastCrouched = timeNow;
    }
    if((bool) player.onGround) {
        currentPlayerState.jumpCount = DJ_head_MAX_JUMPCOUNT-1;
    } else if (DJ_head_WALLJUMP_REFRESHS && (bool) player.onWall && (bool) inputs.jump) {
        currentPlayerState.jumpCount = DJ_head_MAX_JUMPCOUNT-1;
    } 
    if((bool) inputs.jump) {
        if(!(bool)currentPlayerState.hasJumped  && (num)currentPlayerState.jumpCount > 0 && !(bool)player.onGround) {
            if (!(bool) player.onWall && DJ_head_WALLJUMP_REFRESHS) {
                if (DJ_head_ENABLE_CROUCHJUMP && ((timeNow - (num) player.timeLastCrouched) < DJ_head_MS_DURATION_UNCROUCH)) {
                    player.velocity.y = Math.lerp(DJ_head_HEIGHT_CROUCHJUMP,DJ_head_HEIGHT_JUMP,(timeNow - (num)player.timeLastCrouched)/DJ_head_MS_DURATION_UNCROUCH);	# "moonjumping" calculation
                } else {
                    player.velocity.y = DJ_head_HEIGHT_JUMP;	
                }
                if (DJ_head_SOUND > 0) {
                    GAME.SOUND.play2D(32576, 1, 0.5, false); # jump DJ_head_SOUND
                }
                (num)currentPlayerState.jumpCount--;
            }
        }
        currentPlayerState.hasJumped = true;
    }
    else {
        currentPlayerState.hasJumped = false;
    }

    # Sync saving old player states
    addTo playerStateHistory {
        # Data to use in an attempt to find the correct state in history
        position: {x: player.position.x, y: player.position.y, z: player.position.z },
        velocity: {x: player.velocity.x, y: player.velocity.y, z: player.velocity.z },
        # Actual data we want to save
        jumpCount: currentPlayerState.jumpCount,
        hasJumped: currentPlayerState.hasJumped,
        time: GAME.TIME.now(),
        timeLastCrouched: player.timeLastCrouched
    };
    updateState.lastUpdate = GAME.TIME.now();    
}

public action onPlayerSpawn(str id) {
    updateState.updateId = 0;
    updateState.playerUpdateId = 0;
}