# ===================================================================
# File: libs\game\custommovement\doublejump\djregistersync\djump_dash.krnk
# Author: blandrice
# Description: double-jump using registersync feature
# ===================================================================

#include <DJDashconfig_h.krnk>



# Add this both on the server and client side.

public action onPlayerSpawn(str id) {
	obj player = GAME.PLAYERS.findByID(id);
    player.timeLastCrouched = false;
    player.jumpCount = false;
    player.hasJumped = false;
    player.registerSyncValues("timeLastCrouched");
    player.registerSyncValues("timeLastOnGround");
    player.registerSyncValues("jumpCount");
    player.registerSyncValues("hasJumped");
    # dashing
    player.tLastDash = 50000;
    player.haltedDash = false;
    player.hasCrouched = false;
    player.hasDashed = false;
    player.movDir = 0;
    player.dashRot = 0;
    player.plrLastY = 0;
    player.registerSyncValues("tLastDash");
    player.registerSyncValues("haltedDash");
    player.registerSyncValues("hasCrouched");
    player.registerSyncValues("hasDashed");
    player.registerSyncValues("movDir");
    player.registerSyncValues("dashRot");
    player.registerSyncValues("plrLastY");
}

public action onPlayerUpdate(str id, num delta, obj inputs) {
	obj player = GAME.PLAYERS.findByID(id);
	
    # Double-Jump stuff

    # crouch timestamp
    if ((bool) inputs.crouch) 
    {
        player.timeLastCrouched = 0;
    } 
    else 
    {
        (num) player.timeLastCrouched += delta;
    }
    # reset jumpcount on ground
    if((bool) player.onGround) 
    {
        player.jumpCount = (num) DJDashconfig_h_c.MAX_JUMPCOUNT-1;
        player.timeLastOnGround = 0;
        player.hasDashed = false;
    } 
    else
    {
        (num) player.timeLastOnGround += delta;
    } 
    #  reset jumpCount after walljump
     if ((bool) DJDashconfig_h_c.WALLJUMP_REFRESHS && (bool) player.onWall && (bool) inputs.jump) 
    {
        player.jumpCount = (num) DJDashconfig_h_c.MAX_JUMPCOUNT-1;
    } 
    # input jump logic
    if((bool) inputs.jump) {
        if(!(bool)player.hasJumped  && (num)player.jumpCount > 0 && !(bool)player.onGround && ((num)player.timeLastOnGround > 150)) 
        {
            if (!(bool) player.onWall && (bool)DJDashconfig_h_c.WALLJUMP_REFRESHS) 
            {
                if ((bool) DJDashconfig_h_c.ENABLE_CROUCHJUMP && ((num)player.timeLastCrouched < (num)DJDashconfig_h_c.MS_DURATION_UNCROUCH)) 
                {
                    player.velocity.y = Math.lerp((num) DJDashconfig_h_c.VEL_CROUCHJUMP,(num) DJDashconfig_h_c.VEL_JUMP,(num)player.timeLastCrouched/(num) DJDashconfig_h_c.MS_DURATION_UNCROUCH);	# "moonjumping" calculation
                } else 
                {
                    player.velocity.y = (num) DJDashconfig_h_c.VEL_JUMP;	
                }
                if ((num) DJDashconfig_h_c.SOUND > 0) 
                {
                    GAME.SOUND.play2D(32576, 1, 0.5, false); # jump DJDashconfig_h_c.SOUND
                }

                (num)player.jumpCount--;
                player.tLastDash = 50000;
                player.haltedDash = true;
                player.hasDashed = false;
            }
        }
        player.hasJumped = true;
    }
    else {
        player.hasJumped = false;
    }

    GAME.log("tLastDash",(num) player.tLastDash);
    GAME.log("hasCrouched",(bool) player.hasCrouched);
    GAME.log("hasDashed",(bool) player.hasDashed);
    # input dash logic
    if((bool)inputs.crouch && !(bool) player.onGround){
        if((!(bool)player.hasCrouched) && (!(bool)player.hasDashed)) {
            player.hasCrouched = true;
            player.hasDashed = true;
            player.tLastDash = 0;
            GAME.log("init dash");
            if((str) inputs.movDir == 'undefined'){
                player.movDir = 0;
            } else {
                player.movDir = Math.PI2 - ((num)inputs.movDir + Math.PI/2);
            }
            
            player.dashRot = (num)player.rotation.x + (num) player.movDir;
            
            # dashRot = (num)player.rotation.x;
            player.haltedDash = false;
        }
    } else {
        player.hasCrouched = false;
    }
    (num) player.tLastDash += delta;
    if ((num) player.tLastDash < (num)DJDashconfig_h_c.MS_DURATION_DASH) {
        player.position.y = player.plrLastY;
        player.velocity.x = -(num)DJDashconfig_h_c.VEL_DASH * Math.sin((num)player.dashRot);
        player.velocity.z = -(num)DJDashconfig_h_c.VEL_DASH * Math.cos((num)player.dashRot);
        
    } else if (!(bool)player.haltedDash){
        player.velocity.x = (num)player.velocity.x / (num)DJDashconfig_h_c.HALTRATIO;
        player.velocity.z = (num)player.velocity.z / (num)DJDashconfig_h_c.HALTRATIO;
        
        player.haltedDash = true;
    } else if ((num)player.tLastDash < (num)DJDashconfig_h_c.MS_DURATION_DASH + (num)DJDashconfig_h_c.MS_DURATION_HOVER) {
        # player.position.y = player.plrLastY;
        player.velocity.y = 0;
    } else {
        player.plrLastY = (num)player.position.y;
    }
}